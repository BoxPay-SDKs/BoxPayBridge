buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['ReactNativeModule_' + name]
  }

  repositories {
    google()
    mavenCentral()
    maven {
      url("https://oss.sonatype.org/content/repositories/snapshots")
    }
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["ReactNativeModule_" + name]).toInteger()
}

android {
  namespace "com.reactnativemodule"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni"
      ]
    }
  }
}

repositories {
  mavenCentral()
  google()
  // Needed for react-native dependencies
  maven {
    url("https://oss.sonatype.org/content/repositories/snapshots")
  }
}

def kotlin_version = getExtOrDefault("kotlinVersion")
def reactNativeVersion = "0.78.0"

dependencies {
  implementation "com.facebook.react:react-android:$reactNativeVersion"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation(project(":shared"))
}
